referencing Pure

List class: <<Element>ListIterator>Iterable {
	containing Element
	var length: Int
	var: Node? = null {
		head
		tail
	}

	init

	init(...values: ...Element) {
		loop over values as value
			append(value)
	}

	init(value: Element, length) {
		loop over Range(length)
			append(value)
	}

	to {
		prepend(element: Element) {
			val node = Node(element)
			node.nextNode = head
			head?.previousNode = node
			head = node
		}

		insert(index: Int, element: Element) {
			if index == 0 {
				prepend(element)
				return
			}
			if index == length {
				append(element)
				return
			}
			var currentIndex = 1
			var previousNode: Node? = head
			loop while currentIndex < index {
				previousNode = previousNode?.nextNode
				currentIndex++
			}
			val nextNode = previousNode?.nextNode
			val node = Node(element)
			node.previousNode = previousNode
			previousNode?.nextNode = node
			node.nextNode = nextNode
			nextNode?.previousNode = node
		}

		append(element: Element) {
			val node = Node(element)
			node.previousNode = tail
			tail?.nextNode = node
			tail = node
		}

		remove(element: Element) {
			loop over this as node {
				if node.element == element {
					node.previousNode.nextNode = node.nextNode
					node.nextNode.previousNode = node.previousNode
					node.previousNode = null
					node.nextNode = null
				}
			}
		}
	}
		
	overriding to createIterator(): <Element>ListIterator {
		return ListIterator(this)
	}

	operator {
		[index: Int]: Element {
			loop over this as currentIndex, node
				if currentIndex == index
					return node.element
			raise Exception("The index '$index' is outside of the lists length.")
		}

		[index: Int](element: Element) {
			var currentIndex = 0
			var node: Node? = head
			loop while currentIndex < index {
				node = node?.nextNode
				currentIndex++
			}
			node?.element = element
		}
	}

	Node class {
		var: Node? = null {
			previousNode
			nextNode
		}
		var element: Element

		init(element)
	}
	
	ListIterator class: <Int>IndexIterator & <Element>ValueIterator {
		containing Element
		val list: <Element>List
		overriding var currentIndex: Int = 0
		overriding val currentValue gets list[currentIndex]
		overriding val isDone gets currentIndex >= list.length
		
		init(list)
		
		overriding to advance() {
			currentIndex++
		}
	}
}
