referencing Pure

List class: <Iterator>Iterable {
	containing Element
	var length = 0
	var: Node? = null {
		head
		tail
	}

	init

	init(...values: ...Element) {
		loop over values as value {
			append(value)
			length++
		}
	}

	init(value: Element, length) {
		loop over Range(length)
			append(value)
	}

	to {
		prepend(element: Element) {
			val node = Node(element)
			node.nextNode = head
			head?.previousNode = node
			head = node
		}

		insert(index: Int, element: Element) {
			if index == 0 {
				prepend(element)
				return
			}
			if index == length {
				append(element)
				return
			}
			var currentIndex = 1
			var previousNode: Node? = head
			loop while currentIndex < index {
				previousNode = previousNode?.nextNode
				currentIndex++
			}
			val nextNode = previousNode?.nextNode
			val node = Node(element)
			node.previousNode = previousNode
			previousNode?.nextNode = node
			node.nextNode = nextNode
			nextNode?.previousNode = node
		}

		append(element: Element) {
			val node = Node(element)
			node.previousNode = tail
			tail?.nextNode = node
			tail = node
		}

		remove(element: Element) {
			var currentNode = head
			loop while currentNode? {
				if currentNode.element == element {
					currentNode.previousNode?.nextNode = currentNode.nextNode
					currentNode.nextNode?.previousNode = currentNode.previousNode
					currentNode.previousNode = null
					currentNode.nextNode = null
					break
				}
				currentNode = currentNode.nextNode
			}
		}
	}

	operator {
		[index: Int]: Element {
			loop over this as currentIndex, element
				if currentIndex == index
					return element
			raise Exception("The index '$index' is outside of the lists length.")
		}

		[index: Int](element: Element) {
			var currentIndex = 0
			var node: Node? = head
			loop while currentIndex < index {
				node = node?.nextNode
				currentIndex++
			}
			node?.element = element
		}
	}
		
	overriding to createIterator(): Iterator {
		return Iterator()
	}

	bound Node class {
		var: Node? = null {
			previousNode
			nextNode
		}
		var element: Element

		init(element)
	}
	
	bound Iterator class: <Int>IndexIterator & <Element>ValueIterator {
		overriding var currentIndex: Int = 0
		overriding val currentValue gets this<List>[currentIndex]
		overriding val isDone gets currentIndex >= length
		
		overriding to advance() {
			currentIndex++
		}
	}
}
