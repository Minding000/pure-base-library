referencing Pure

Map class: <Iterator>Iterable {
	containing Key, Value
	var size: Int
	val keys = <Key>List()
	val values = <Value>List()

	operator {
		[key: Key]: Value {
			loop over keys as index, currentKey {
				if currentKey == key
					return values[index]
			}
			raise Exception("Key '$key' does not exist.")
		}

		[key: Key](value: Value) {
			loop over keys as index, currentKey {
				if currentKey == key {
					values[index] = value
					return
				}
			}
			keys.append(key)
			values.append(value)
		}
	}
		
	overriding to createIterator(): Iterator {
		return Iterator()
	}
	
	bound Iterator class: <Key>KeyIterator & <Value>ValueIterator {
		val keyIterator = keys.createIterator()
		overriding val currentKey gets keyIterator.currentValue
		overriding val currentValue gets this[currentKey]
		overriding val isDone gets keyIterator.isDone
		
		overriding to advance() {
			keyIterator.advance()
		}
	}
}
